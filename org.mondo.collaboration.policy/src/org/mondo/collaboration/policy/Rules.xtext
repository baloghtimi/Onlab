grammar org.mondo.collaboration.policy.Rules with org.eclipse.xtext.common.Terminals

generate rules "http://www.mondo.org/collaboration/policy/Rules"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/viatra/query/patternlanguage/PatternLanguage" as iql

Model:
	roles+=Role*
	policy=Policy;
	
Policy:
	'policy' name=ID access=AccessibilityLevel operation=OperationType 'by' 'default' '{'
	rules+=Rule*
	'}' 'with' resolution=ResolutionType 'resolution';
	
Rule:
	'rule' name=ID access=AccessibilityLevel operation=OperationType 'to' roles+=[Role] (',' roles+=[Role])* '{'
	'select' asset=Asset
	'from' 'query' query=Query 
	bindings+=Binding*
	'}' ('with' priority=INT 'priority')?;
	
Role:
	User | Group;
	
User:
	'user' name=ID;
	
Group:
	'group' name=ID '{'  users+=[User] (',' users+=[User])* '}'
;
	
Asset:
	ObjectFact | ReferenceFact;
	
ObjectFact:
	'obj(' parameter=Parameter ')';
	
ReferenceFact:
	'ref(' sourceParam=Parameter '->' '->' targetParam=Parameter ')';
	
Query:
	pattern=[iql::Pattern|STRING];
	
Binding:
	'where' parameter=Parameter 'bound' 'to' bind=Bind;
	
Parameter:
	variable=[iql::Variable];
	
Bind:
	ObjectBind; //| ClassBind;

ObjectBind:
	object=[ecore::EObject];
	
/*ClassBind:
	class=[ecore::EClass];*/
	
enum AccessibilityLevel:
	ALLOW='allow' | DENY='deny';
	
enum OperationType:
	READ='R' | WRITE='W' | READWRITE='RW';
	
enum ResolutionType:
	RESTRICTIVE='restrictive' | PERMISSIVE='permissive';
